BEGIN {
  print "/* Generated by dmpstruct.awk */"
  print "#ifndef EMACS_DMPSTRUCT_H"
  print "#define EMACS_DMPSTRUCT_H"
  struct_name = ""
  tmpfile = "dmpstruct.tmp"
}
# Match a type followed by optional syntactic whitespace
/^(enum|struct|union) [a-zA-Z0-9_]+([\t ]|\/\*.*\*\/)*$/ {
  struct_name = $2
  close (tmpfile)
}
/^(enum|struct|union) [a-zA-Z0-9_]+([\t ]|\/\*.*\*\/)*$/, /^(  )?};$/ {
  print $0 > tmpfile
}
/^(  )?} *(GCALIGNED_STRUCT)? *;$/ {
  if (struct_name != "") {
    fflush (tmpfile)
    cmd = "../lib-src/make-fingerprint -r " tmpfile
    cmd | getline hash
    close (cmd)
    printf "#define HASH_%s_%.10s\n", struct_name, hash
    struct_name = ""
  }
}
END {
  print "#endif /* EMACS_DMPSTRUCT_H */"
}
