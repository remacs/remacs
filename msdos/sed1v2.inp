# -sed1v2.inp-----------------------------------------------------------
# Configuration script for src/Makefile under DJGPP v2.x
# ----------------------------------------------------------------------
#
# Copyright (C) 1996, 1997, 1999, 2000, 2001, 2002, 2003, 2004,
#   2005, 2006, 2007, 2008, 2009, 2010, 2011, 2012  Free Software Foundation, Inc.
#
# This file is part of GNU Emacs.
#
# This file is free software; as a special exception, the author gives
# unlimited permission to copy and/or distribute it, with or without
# modifications, as long as this notice is preserved.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.
#
# ----------------------------------------------------------------------
s/^#.*//
s/^[ \f\t][ \f\t]*$//
s/^ /	/
s/\.h\.in/.h-in/
/^	LC_ALL=C \$(RUN_TEMACS)/i\
	stubedit temacs.exe minstack=1024k
/^	LC_ALL=C.*\$(RUN_TEMACS)/s/LC_ALL=C/set &;/
/-batch -l loadup/a\
	stubify emacs\
	stubedit emacs.exe minstack=2048k
/^MAKE *=/s/^/# /
/^SHELL *=/s/^/# /
/^srcdir *=/s/@[^@\n]*@/./
/^VPATH *=/s/@[^@\n]*@/./
/^CC *=/s/@[^@\n]*@/gcc/
/^CPP *=/s/@[^@\n]*@/gcc -e/
/^CFLAGS *=/s/@[^@\n]*@/-O2 -gcoff/
/^ALL_CFLAGS *=/s/@[^@\n]*@//g
/^CPPFLAGS *=/s/@[^@\n]*@//
/^LDFLAGS *=/s/@[^@\n]*@//
/^LIBS *=/s/@[^@\n]*@//
/^LIBES *=/,/^ *$/ {
  s/@[^@\n]*@//g
}
/^LIBOBJS *=/s/@[^@\n]*@/getloadavg.o/
/^GETLOADAVG_LIBS *=/s/@[^@\n]*@//
/^LIBSOUND *=/s/@LIBSOUND@//
/^CFLAGS_SOUND *=/s/@CFLAGS_SOUND@//
/^RSVG_LIBS *=/s/@RSVG_LIBS@//
/^RSVG_CFLAGS *=/s/@RSVG_CFLAGS@//
/^OTHER_FILES *=/s/@OTHER_FILES@//
/^EXEEXT *=/s/@EXEEXT@/.exe/
/^version *=/s/@[^@\n]*@//
/^LN_S *=/s/@[^@\n]*@/ln -s/
/^M_FILE *=/s!@[^@\n]*@!m/intel386.h!
/^S_FILE *=/s!@[^@\n]*@!s/msdos.h!
/^@SET_MAKE@$/s/@SET_MAKE@//
/^.\${libsrc}make-docfile.*>/s!make-docfile!make-docfile -o ../etc/DOC!
/^.\${libsrc}make-doc/s!>.*$!!
/^[ 	]*$/d
/^temacs:/s/prefix-args//
/^temacs:/s/stamp-oldxmenu//
/^RUN_TEMACS *=/s|`/bin/pwd`|.|
/^	mv \.\/\.gdbinit/d
/^	if test -f/c\
	command.com /c if exist .gdbinit rm -f _gdbinit
/^	else mv \.\/\.gdbinit/d
/^	#/d
/^	cd.*make-docfile/s!$!; cd ${dot}${dot}/src!
/^	@: /d
/^	-\{0,1\}ln -/s/ln -f/cp -pf/
/^[ 	]touch /s/touch/djecho $@ >/
s/bootstrap-emacs/b-emacs/
s/bootstrap-temacs/b-temacs/
s/bootstrap-doc/b-doc/
/rm -f bootstrap-emacs/s/b-emacs/b-emacs b-emacs.exe/
s/echo.*buildobj.lst/dj&/
/ --load loadup bootstrap/i\
	stubedit temacs.exe minstack=1024k
/ --load loadup bootstrap/a\
	stubify emacs
/^	mv -f emacs/a\
	stubedit b-emacs.exe minstack=3072k\
	djecho bootlisp > bootlisp
/^	-\{0,1\}rm -f/s/\\#/#/
/^	@\{0,1\}cd ..\/lisp; /s|$|\; cd ../src|
/^	echo.* buildobj.h/s|echo |djecho |

# arch-tag: c7e3aacb-4162-460e-99f9-4252bca68d2c
